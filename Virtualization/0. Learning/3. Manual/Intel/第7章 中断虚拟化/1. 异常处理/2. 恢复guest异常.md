
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [1. 直接恢复](#1-直接恢复)
  - [1.1. 处理NMI unblocking位](#11-处理nmi-unblocking位)
- [2. 示例](#2-示例)
  - [2.1. 监控SYSENTER指令的原理及实现](#21-监控sysenter指令的原理及实现)
  - [2.2. 处理WRMSR指令产生的VM-exit](#22-处理wrmsr指令产生的vm-exit)
  - [2.3. 处理`#PF`异常](#23-处理pf异常)
  - [2.4. guest 代码](#24-guest-代码)
- [3. 恢复原始向量事件](#3-恢复原始向量事件)

<!-- /code_chunk_output -->

当guest引发异常的条件是host/VMM所设置时, VMM取消引发guest 异常的条件后使用 VMRESUME 指令恢复 guest 的运行.

# 1. 直接恢复

当异常直接引发`VM-exit`, 也就是 `IDT-vectoring information` 字段`bit 31`为0 (不是间接引发`VM-exit`)时. 尽管 `VM-exit interruption information` 字段记录了引发VM-exit的异常. 但是, 由于异常是host的原因引发, 并不是guest 造成的, 因此VMM无须反射异常给guest处理(忽略这个异常), 可以直接执行 VMRESUME 指令恢复guest 执行. 

## 1.1. 处理NMI unblocking位

当 `VM-exit interruption information` 字段的“`NMI unblocking`"位为1时, 表明由于尝试执行IRET指令而解除了“blocking by NMI"阻塞状态, 但是IRET指令并没有成功执行. 

在这种情况下, VMM在执行VMRESUME指令前, 需要将interruptility state字段bit3置1, 用来反馈guest在执行IRET指令前存在“blocking by NMI”阻塞状态. 恢复guest执行后, guest 完成IRET指令的执行将自动解除"blocking by NMI"阻塞状态. 当pin-based VM-execution control 字段的“`virtual NMIs`"为1 (“`NMI exiting`"也为1), VM-exit interruption information 字段的“NMI unblocking" 为1时, 表明尝试执行IRET指令而解除了"blocking by virtual-NM1"阻塞状态. 

同样, VMM需要将 `interrupibility state` 字段bit 3置1,反馈guest在执行IRET指令前存在 "`blocking by virtual-NMI`" 阻塞状态, 然后执行VMRESUME指令恢复guest执行. 

注意: 当 `VM-exit interruption information` 字段记录的向量号为8 (`#DF`异常)时, `VM-exit interruption information` 字段的 `bit 12` 是未定义(参见3.10.2.1节), VMM不需要对`interruptibility state` 字段进行额外的设置. 

# 2. 示例

>实现对SYSENTER指令的监控

现在, 我们用一个例子来说明VMM**直接恢复guest 执行**而**忽略产生的异常事件**, 这个例子实现了对guest 用户层代码执行**SYSENTER指令**的监控. 

本例子的代码在`chap07\ex7-1`目录下. 

## 2.1. 监控SYSENTER指令的原理及实现

用户层代码通过SYSENTER指令来快速切入kernel的服务例程, 目标代码人口的线性地址提供在 `IA32_SYSENTER_EIP` 寄存器里. VMM通过设置`IA32_SYSENTER_EIP` 寄存器在 **MSR write bitmap** 区域里对应的位**置1**, 来监控guest更新 `IA32_SYSENTER_EIP` 寄存器. 

当 guest 尝试使用**WRMSR指令**向`IA32_SYSENTER_EIP`寄存器写人系统服务例程人口地址时产生`VM-exit`. VMM将拦截 guest 的写人动作, 使用一个预先定义的**签名值**来代替这个系统服务例程人口地址值**写人** `IA32_SYSENTER_EIP` 寄存器. 

当guest 执行SYSENTER指令时, 由于**目标地址无效**(写人的**签名值**)而产生`#PF`**异常！！！**, VMM拦截了`#PF`异常后(由于`#PF`异常而**导致VM-exit！！！**), **VMM**检查引发`#PF`异常的**源地址值**是否为这个**签名值**, 如果属于则找回guest**原系统服务例程的人口地址**, 直接**恢复guest的执行**. 

代码片段7-1:

```x86asm
;;
;; 定义一个 SYSENTER_EIP hook 签名
;;
%define SYSENTER_HOOK_SIGN                      'HOOK'
```

在 ex.asm 文件的开头, 我们定义一个`SYSENTER_HOOK_SIGN`常量作为`hook IA32_SYSENTER_EIP` 寄存器的签名值. 

代码片段7-2:

```x86asm
;;
;; 设置拦截对 IA32_SYSENTER_EIP 的写操作
;;
mov esi, IA32_SYSENTER_EIP
call set_msr_write_bitmap
```

在 `init_guest_a` 与 `init_guest_b` 函数里, 调用 `set_msr_write_bitmap` 函数将 `IA32_SYSENTER_EIP` 寄存器在**MSR write bitmap**对应位**置位**. 

代码片段7-3:

```x86asm
;;
;; 注册另一个 WRMSR 指令处理例程: Ex.DoWRMSE
;;
mov esi, Ex.DoWRMSR
mov [DoVmExitRoutineTable + EXIT_NUMBER_WRMSR * 4], esi

;;
;; 注册另一个 VMM 的 page fault 处理例程: Ex.DoPageFault
;;                
mov esi, Ex.DoPageFault
mov [DoExceptionTable + 14 * 4], esi
```

接下来, 在 `TargetCpuVmentry` 函数里分别注册 `WRMSR` 与 `#PF` 的处理例程: `Ex.DoWRMSR` 与 `Ex.PageFault` 函数. 当由于**WRMSR指令**与`#PF`异常而产生VM-exit时, VMM将**调用它们**来进行相关的处理. 

## 2.2. 处理WRMSR指令产生的VM-exit

**guest**尝试写 `IA32 SYSENTER EIP` 寄存器时将产生`VM-exit`, VMM将替换guest想要写人的值. 

代码片段7-4:

```x86asm
;----------------------------------------------
; Ex 模块的 WRMSR 处理例程　
;----------------------------------------------
Ex.DoWRMSR:
        push R5
        push R3
        push R1
        push R2
        mov R5, [gs: PCB.Base]
        
        ;;
        ;; 当前 VM store block
        ;;
        mov R3, [R5 + PCB.CurrentVmbPointer]
        mov R3, [R3 + VMB.VsbBase]
        
        ;;
        ;; 检查是否属于 IA32_SYSENTER_EIP
        ;;
        mov eax, [R3 + VSB.Rcx]
        cmp eax, IA32_SYSENTER_EIP
        jne Ex.DoWRMSR.@1
        
        ;;
        ;; 设置 IA32_SYSENTER_EIP 值为 SYSENTER_HOOK_SIGN, 用于验证
        ;;
        SetVmcsField    GUEST_IA32_SYSENTER_EIP, SYSENTER_HOOK_SIGN
        
        ;;
        ;; 保存原 IA32_SYSENTER_EIP 
        ;;
        mov eax, [R3 + VSB.Rax]
        mov edx, [R3 + VSB.Rdx]
        mov [Ex.SysenterEip], eax
        mov [Ex.SysenterEip + 4], edx

        DEBUG_RECORD    "[Ex.DoWRMSR]: set SYSENTER_HOOK_SIGN !"
                
        jmp Ex.DoWRMSR.@2
        
Ex.DoWRMSR.@1:       
        ;;
        ;; 配置 VM-entry/VM-exit MSR-load/MSR-store
        ;;                         
        mov esi, eax
        mov eax, [R3 + VSB.Rax]
        mov edx, [R3 + VSB.Rdx]
        call append_vmentry_msr_load_entry        
        
Ex.DoWRMSR.@2:
        ;;
        ;; 更新 guest-RIP
        ;;
        call update_guest_rip        
        
        mov eax, VMM_PROCESS_RESUME
        pop R2
        pop R1
        pop R5
        pop R3
        ret
```

`Ex.DoWRMSR` 函数从当前的**VSB** (VM store block)块里**读取guest**的**RCX寄存器值**, 然后判断是否为 `IA32_SYSENTER_EIP` 寄存器. 

(1) 是, 则使用 `SetVmcsField` 宏设置**guest**的`IA32_SYSENTER_EIP`字段值为预定义的签名值. 并且**读取guest**的**RAX**与**RDX**寄存器值, 保存**guest想要写人的值**. 

(2) 否, 则读取guest的RAX与RDX寄存器值, 使用`append_vmentry_msr_load_entry`函数在`MSR-load`列表里添加这个MSR的表项, 在`VM-entry`时加载guest的MSR内容. 

最后, 调用update_guest_rip函数跳过guest的WRMSR指令, 恢复guest的下一条指令执行. 注意, VMM必须要保存 guest 写人的原值, 以便后续处理及恢复. `libl\Vmx\VmxMsr.asm`文件里也实现了一个`AppendMsrVte`函数用来保存写人MSR的原值. 

## 2.3. 处理`#PF`异常

guest在尝试执行SYSENTER指令时产生`#PF`异常从而导致VM-exit.  但是, 这个#PF异常并**不是由guest 本身原因**而导致的. 

VMM的处理如代码片段7-5所示. 

代码片段7-5:

```x86asm
;----------------------------------------------
; Ex 模块的 page fault 处理例程　
;----------------------------------------------
Ex.DoPageFault:
        push R5
        push R2        
        mov R5, [gs: PCB.Base]

        ;;
        ;; 读取引发 #PF 的线性地址
        ;;
        mov R0, [R5 + PCB.ExitInfoBuf + EXIT_INFO.ExitQualification]
                
        ;;
        ;; 检查线性地址是否由 VMM 设置的地址值        
        ;;
        cmp R0, SYSENTER_HOOK_SIGN
        mov eax, VMM_PROCESS_DUMP_VMCS
        jne Ex.DoPageFault.Reflect

        ;;
        ;; 读取原 IA32_SYSENTER_EIP 值
        ;;                
        mov R0, [Ex.SysenterEip]
        
        DEBUG_RECORD    "[Ex.DoPageFault]: call SysRoutine !"
        
        ;;
        ;; 更新 guest-RIP
        ;;
        SetVmcsField    GUEST_RIP, R0
        
        jmp Ex.DoPageFault.Done

Ex.DoPageFault.Reflect:
        ;;
        ;; 反射 #PF 异常给回 guest
        ;;
        SetVmcsField    VMENTRY_INTERRUPTION_INFORMATION, INJECT_EXCEPTION_PF
        mov eax, [R5 + PCB.ExitInfoBuf + EXIT_INFO.InterruptionErrorCode]
        SetVmcsField    VMENTRY_EXCEPTION_ERROR_CODE, eax
        
Ex.DoPageFault.Done:
        mov eax, VMM_PROCESS_RESUME
        pop R2
        pop R5        
        ret
```

`Ex.DoPageFault`函数读取引发`#PF异常`的**线性地址值**, 判断是否为我们所设置的**签名值**. 

(1)是, 则读取guest想写人`IA32_SYSENTER_EIP`寄存器的原值. 使用`SetVmcsField`将这个值写入guest RIP字段, 从而直接跳转到目标的服务例程里执行. 

(2)否, 则需要注人一个`#PF`异常给guest处理. 

注意: 当 `Ex.DoPageFault` 函数检查到不是VMM所设置的条件而引起 `#PF` 异常时, VMM应该反射`#PF`异常交回guest处理. 这里直接设置`VM-entry interruption information`字段, 复制错误码到`VM-entry interruption error code`字段里. 

## 2.4. guest 代码

下面是guest的部分代码, 实现在`chap07/ex7-1/guest_ex.asm`文件里. 

代码片段7-6:



# 3. 恢复原始向量事件

